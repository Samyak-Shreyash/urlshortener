plugins {
    id 'application'
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
}

application {
    mainClass = 'com.systemdesign.urlshortener.UrlshortenerApplication'
}

group = 'com.systemdesign'
version = '0.0.2-SNAPSHOT'
description = 'System Design Project to manipulate URL on Demand'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// Spring Boot Docker Build (optional - remove if causing issues)
bootBuildImage {
    imageName = "url-shortener:${version}"
    environment = ["BP_JVM_VERSION": "21"]
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.security:spring-security-crypto'
    
    implementation 'com.mysql:mysql-connector-j:9.3.0'
    
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Manual Docker build tasks
task buildDockerImage(type: Exec) {
    dependsOn bootJar
    commandLine 'docker', 'build', '-t', "url-shortener:${version}", '.'
}

task buildDockerImageAlpine(type: Exec) {
    dependsOn bootJar
    commandLine 'docker', 'build', '-f', 'Dockerfile.app', '-t', "url-shortener:${version}-alpine", '.'
}

// Clean task to remove build artifacts
clean {
    delete fileTree(dir: 'logs', include: '**/*')
}